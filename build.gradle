plugins {
    id "se.inera.intyg.plugin.common" version "1.0.62" apply false
    id "org.akhikhl.gretty" version "1.4.2" apply false
    id "com.moowork.node" version "1.1.1" apply false
}

ext {
    apacheHttpClientVersion = "4.5.1"
    aspectjWeaverVersion = "1.8.9"
    commonsIoVersion = "2.5"
    jacksonVersion = "2.9.4"
    logbackVersion = "1.0.13"
    opensamlVersion = "2.6.1"
//    openWsVersion = "1.4.2-1"

    slf4jVersion = "1.7.10"
    springContextVersion = "5.0.5.RELEASE"
    springSecurityVersion = "4.2.5.RELEASE"
    springSecurityExtensionsVersion = "1.0.3.RELEASE"

}

allprojects {
    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: 'maven'
    apply plugin: 'idea'

    repositories {
        mavenLocal()
        maven { url "https://build-inera.nordicmedtest.se/nexus/repository/releases/" }
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    dependencies {
        compile "ch.qos.logback:logback-classic:${logbackVersion}"
        compile "org.slf4j:slf4j-api:${slf4jVersion}"

        testCompile 'junit:junit:4.+'
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
    }

    group = 'se.inera.intyg.authsampleapp'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://build-inera.nordicmedtest.se/nexus/repository/releases/") {
            authentication(userName: System.properties['nexusUsername'], password: System.properties['nexusPassword'])
        }
    }

    [install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.findAll {dep -> dep.groupId == 'se.inera.intyg.infra'}.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.infra')
        }
    }
}

task tagRelease(type: TagReleaseTask)
